# Alexa

## ðŸ“ IntroduÃ§Ã£o

Este projeto visa desenvolver um programa similar a uma assistente virtual (tipo "Alexa") na plataforma EdSim51. O sistema recebe comandos via comunicaÃ§Ã£o serial, armazena-os em memÃ³ria e executa aÃ§Ãµes especÃ­ficas baseadas no comando fornecido.

## ðŸš€ Funcionalidades do Sistema

### InicializaÃ§Ã£o

```assembly
RS equ P1.3 
EN equ P1.2

ORG 0000h
    ACALL BV_MENSAGEM
    LJMP INICIO
ORG 0100h

INICIO:
    MOV SCON, #01010000B ; Configurando SCON
    MOV PCON, #10000000B ; Ativa o SCON
    MOV TMOD, #20H       ; CT1 no modo 2
    MOV TH1, #243        ; Valor para a recarga
    MOV TL1, #243        ; Valor para a primeira contagem
    CLR ES               ; Desativa interrupÃ§Ã£o serial
    SETB EA              ; Se precisar das demais
    SETB TR1
    MOV R0, #30H
    MOV R1, #29H
```

**ExplicaÃ§Ã£o:** Este bloco inicializa os pinos e bits necessÃ¡rios para o funcionamento da comunicaÃ§Ã£o serial, alÃ©m de exibir uma mensagem de boas-vindas no display LCD.

### Captura de Comandos

```assembly
SALVAR_ACAO:
    JNB RI, SALVAR_ACAO
    CLR RI
    MOV A, SBUF
    ; Verifica se sÃ£o caracteres de validaÃ§Ã£o do ASCII como enters e /r
    CJNE A, #0DH, VERIFICA_LF
    SJMP SALVAR_ACAO    
    
VERIFICA_LF:
    ; Verifica se Ã© LF (0A)
    CJNE A, #0AH, SALVAR_CARACTERE
    SJMP SALVAR_ACAO    ; Ignora LF e volta para esperar prÃ³ximo caractere

SALVAR_CARACTERE:
    MOV @R0, A
    CJNE A, #'$', CONTINUAR
    SJMP FIM

CONTINUAR:
    INC R0
    SJMP SALVAR_ACAO
```

**ExplicaÃ§Ã£o:** Com as portas e pinos inicializados, o programa comeÃ§a a "escutar" a comunicaÃ§Ã£o serial. Ao receber uma mensagem, cada caractere Ã© salvo na memÃ³ria atÃ© encontrar o caractere de controle ('$').

### Processamento de Comandos

```assembly
FIM:
    MOV R0, #30H

    MOV DPTR, #msg_horas
    ACALL COMPARAR_STRING
    JZ HORAS

    MOV DPTR, #msg_ligar_luz
    ACALL COMPARAR_STRING
    JZ LUZ

    MOV DPTR, #msg_apagar_luz
    ACALL COMPARAR_STRING
    JZ APAGAR_LUZ

    MOV DPTR, #msg_repouso_pergunta
    ACALL COMPARAR_STRING
    JZ FLAG_REPOUSO

    MOV DPTR, #msg_reiniciar3
    ACALL COMPARAR_STRING
    JZ FLAG_REINICIAR

    MOV DPTR, #msg_ola3
    ACALL COMPARAR_STRING
    JZ FLAG_OLA

    MOV DPTR, #msg_clima
    ACALL COMPARAR_STRING
    JZ FLAG_CLIMA

    ACALL ERRO_MENSAGEM
    LJMP SALVAR_ACAO

FLAG_OLA:
    LJMP OLA
FLAG_CLIMA:
    LJMP CLIMA
FLAG_REINICIAR:
    LJMP REINICIAR
FLAG_REPOUSO:
    LJMP REPOUSO
```

**ExplicaÃ§Ã£o:** ApÃ³s receber a string completa, o sistema compara o comando recebido com as aÃ§Ãµes disponÃ­veis. Se o comando for reconhecido, chama a subrotina responsÃ¡vel por executar a aÃ§Ã£o correspondente; caso contrÃ¡rio, exibe uma mensagem de erro.

## ðŸ“‹ Subrotinas de Comando

### ExibiÃ§Ã£o de Horas

```assembly
HORAS:
    ACALL lcd_init
    ACALL clearDisplay
    ACALL delay_longo
    MOV A, #03H
    ACALL posicionaCursor
    MOV DPTR, #msg_hora
    ACALL escreveString
    MOV A, #46H
    ACALL posicionaCursor
    MOV DPTR, #msg_hora2
    ACALL escreveString
    MOV R0, #30H
    ACALL LIMPAR_REGISTRADORES
    LJMP SALVAR_ACAO
```

**ExplicaÃ§Ã£o:** A subrotina HORAS exibe no display LCD uma mensagem contendo o horÃ¡rio atual.

### Controle de IluminaÃ§Ã£o - Ligar

```assembly
LUZ:
    MOV P0, #00000000B
    ACALL lcd_init
    ACALL clearDisplay
    ACALL delay_longo
    MOV A, #02H
    ACALL posicionaCursor
    MOV DPTR, #msg_ligar
    ACALL escreveString
    MOV A, #42H
    ACALL posicionaCursor
    MOV DPTR, #msg_ok
    ACALL escreveString
    MOV R0, #30H
    ACALL LIMPAR_REGISTRADORES
    LJMP SALVAR_ACAO
```

**ExplicaÃ§Ã£o:** Esta subrotina ativa os LEDs do EdSim51 (P0 = 0) e exibe no display uma mensagem confirmando que as luzes foram acesas.

### Controle de IluminaÃ§Ã£o - Desligar

```assembly
APAGAR_LUZ:
    MOV P0, #11111111B
    ACALL lcd_init
    ACALL clearDisplay
    ACALL delay_longo
    MOV A, #00H
    ACALL posicionaCursor
    MOV DPTR, #msg_desligar
    ACALL escreveString
    MOV R0, #30H
    ACALL LIMPAR_REGISTRADORES
    LJMP SALVAR_ACAO
```

**ExplicaÃ§Ã£o:** Esta subrotina desativa os LEDs (P0 = 1) e exibe uma mensagem confirmando que as luzes foram apagadas.

### Modo Repouso

```assembly
REPOUSO:
    ACALL lcd_init
    ACALL clearDisplay
    ACALL delay_longo
    MOV A, #01H
    ACALL posicionaCursor
    MOV DPTR, #msg_dormir
    ACALL escreveString
    MOV A, #41H
    ACALL posicionaCursor
    MOV DPTR, #msg_dormir2
    ACALL escreveString
    ACALL delay_longo
    ACALL clearDisplay
    ACALL delay_longo
    MOV A, #01H
    ACALL posicionaCursor
    MOV DPTR, #msg_dormir3
    ACALL escreveString
    MOV R0, #30H
    ACALL LIMPAR_REGISTRADORES
    LJMP SALVAR_ACAO
```

**ExplicaÃ§Ã£o:** Esta subrotina simula a entrada no modo de repouso, exibindo uma sequÃªncia de mensagens no LCD.

### ReinicializaÃ§Ã£o do Sistema

```assembly
REINICIAR:
    ACALL lcd_init
    ACALL clearDisplay
    ACALL delay_longo
    MOV A, #01H
    ACALL posicionaCursor
    MOV DPTR, #msg_reiniciar
    ACALL escreveString
    MOV A, #41H
    ACALL posicionaCursor
    MOV DPTR, #msg_reiniciar2
    ACALL escreveString
    ACALL delay_longo
    ACALL LIMPAR_MEMORIA
    ACALL delay_longo
    ACALL clearDisplay
    ACALL delay_longo
    ACALL BV_MENSAGEM
    MOV R0, #30H
    LJMP SALVAR_ACAO
```

**ExplicaÃ§Ã£o:** Esta subrotina reinicia o sistema, limpando a memÃ³ria e os registradores, alÃ©m de reexibir a mensagem de boas-vindas.

### InteraÃ§Ã£o BÃ¡sica - OlÃ¡

```assembly
OLA:
    ACALL lcd_init
    ACALL clearDisplay
    ACALL delay_longo
    MOV A, #01H
    ACALL posicionaCursor
    MOV DPTR, #msg_ola
    ACALL escreveString
    MOV A, #41H
    ACALL posicionaCursor
    MOV DPTR, #msg_ola2
    ACALL escreveString
    MOV R0, #30H
    ACALL LIMPAR_REGISTRADORES
    LJMP SALVAR_ACAO
```

**ExplicaÃ§Ã£o:** Esta subrotina responde ao usuÃ¡rio com uma saudaÃ§Ã£o, tornando a interface mais interativa.

### InformaÃ§Ã£o de Clima/Temperatura

```assembly
CLIMA:
    ACALL lcd_init
    ACALL clearDisplay
    ACALL MEDIR_TEMP 
    ACALL delay_longo
    MOV A, #02H
    ACALL posicionaCursor
    MOV DPTR, #msg_temp
    ACALL escreveString
    ACALL delay_longo

    ; Converte e exibe a temperatura
    MOV A, R4            ; A = temperatura (0â€“255)
    MOV B, #100          ; Dividir por 100 para obter as centenas
    DIV AB               ; A = centenas, B = resto (dezenas e unidades)
    ADD A, #30h          ; Converte centenas para ASCII
    MOV R6, A            ; Armazena a centena (ASCII)

    MOV A, B              ; A = resto (dezenas e unidades)
    MOV B, #10            ; Dividir por 10 para obter as dezenas
    DIV AB                ; A = dezenas, B = unidades
    ADD A, #30h           ; Converte dezenas para ASCII
    MOV R7, A             ; Armazena a dezena (ASCII)

    MOV A, B              ; A = unidades
    ADD A, #30h           ; Converte unidades para ASCII
    MOV R5, A             ; Armazena a unidade (ASCII)

    ; Exibe centenas
    MOV A, #0C4h          ; PosiÃ§Ã£o no LCD: linha 2, coluna 4
    ACALL posicionaCursor
    MOV A, R6
    ACALL sendCharacter

    ; Exibe dezenas
    MOV A, #0C5h          ; PosiÃ§Ã£o no LCD: linha 2, coluna 5
    ACALL posicionaCursor
    MOV A, R7
    ACALL sendCharacter

    ; Exibe unidades
    MOV A, #0C6h          ; PosiÃ§Ã£o no LCD: linha 2, coluna 6
    ACALL posicionaCursor
    MOV A, R5
    ACALL sendCharacter

    ACALL delay_longo
    
    MOV A, #48H
    ACALL posicionaCursor
    MOV DPTR, #msg_temp2
    ACALL escreveString
    MOV R0, #30H
    ACALL LIMPAR_REGISTRADORES
    LJMP SALVAR_ACAO
```

**ExplicaÃ§Ã£o:** Esta subrotina lÃª o valor do componente ADC do EdSim51, converte o valor em volts para temperatura (multiplicando por 2), separa os dÃ­gitos do nÃºmero e os exibe no LCD.

### Tratamento de Erro - Comando InvÃ¡lido

```assembly
ERRO_MENSAGEM:
    ACALL lcd_init
    ACALL clearDisplay
    ACALL delay_longo
    MOV A, #02H
    ACALL posicionaCursor
    MOV DPTR, #msg_erro
    ACALL escreveString
    MOV A, #41H
    ACALL posicionaCursor
    MOV DPTR, #msg_erro2
    ACALL escreveString
    MOV R0, #30H
    ACALL LIMPAR_REGISTRADORES
    RET
```

**ExplicaÃ§Ã£o:** Esta subrotina exibe uma mensagem de erro no display quando o comando fornecido nÃ£o corresponde a nenhuma das aÃ§Ãµes disponÃ­veis.

## ðŸ¤– Resumo de Funcionamento

O sistema opera seguindo estas etapas:

1. Recebe um comando do usuÃ¡rio via comunicaÃ§Ã£o serial
2. Processa e compara o comando com as aÃ§Ãµes disponÃ­veis
3. Se o comando existir, executa a subrotina correspondente
4. Caso contrÃ¡rio, exibe uma mensagem de erro

## ðŸ“‹ Lista de Comandos DisponÃ­veis

| Comando | FunÃ§Ã£o |
|---------|--------|
| **Que horas sao?** | Exibe o horÃ¡rio atual |
| **Como esta o clima?** | Calcula e exibe a temperatura |
| **Ascender as luzes** | Ativa os LEDs |
| **Apagar as luzes** | Desativa os LEDs |
| **Entrar em modo repouso** | Ativa o modo de repouso |
| **Reiniciar** | Reinicia todo o sistema |
| **Ola** | Responde com uma saudaÃ§Ã£o |

---
## ðŸ§¬ Experimentos

![image](https://github.com/Leonardo-Souza-de-Castro/Projeto-arquitetura-de-computadores/blob/main/img/Ola.png)
![image](https://github.com/Leonardo-Souza-de-Castro/Projeto-arquitetura-de-computadores/blob/main/img/Ola_processado.png)
![image](https://github.com/Leonardo-Souza-de-Castro/Projeto-arquitetura-de-computadores/blob/main/img/Ascender.png)
![image](https://github.com/Leonardo-Souza-de-Castro/Projeto-arquitetura-de-computadores/blob/main/img/Apagar.png)

---

## ðŸ“ˆ Diagrama do projeto

```mermaid
flowchart TD
    A[InÃ­cio] --> B[ConfiguraÃ§Ã£o Inicial]
    B --> C[BV_MENSAGEM - Exibe mensagem de boas-vindas]
    C --> D[ConfiguraÃ§Ã£o da ComunicaÃ§Ã£o Serial]
    D --> E[SALVAR_ACAO]
    
    E -->|Recebe Caractere| F{Caractere = CR?}
    F -->|Sim| E
    F -->|NÃ£o| G{Caractere = LF?}
    G -->|Sim| E
    G -->|NÃ£o| H[SALVAR_CARACTERE]
    
    H --> I{Caractere = '$'?}
    I -->|Sim| J[FIM - Processa comando]
    I -->|NÃ£o| K[Incrementa Ponteiro]
    K --> E
    
    J --> L{Compara com comandos conhecidos}
    
    L -->|"Que horas sao?"| M[HORAS]
    L -->|"Como esta o clima?"| N[CLIMA]
    L -->|"Ascender as luzes"| O[LUZ]
    L -->|"Apagar as luzes"| P[APAGAR_LUZ]
    L -->|"Entrar em modo repouso"| Q[REPOUSO]
    L -->|"Reiniciar"| R[REINICIAR]
    L -->|"Ola"| S[OLA]
    L -->|Comando Desconhecido| T[ERRO_MENSAGEM]
    
    M --> U[Exibe hora atual no LCD]
    N --> V[Mede temperatura e exibe no LCD]
    O --> W[Liga LEDs e exibe confirmaÃ§Ã£o]
    P --> X[Desliga LEDs e exibe confirmaÃ§Ã£o]
    Q --> Y[Exibe mensagem de repouso]
    R --> Z[Reinicia o sistema]
    S --> AA[Exibe saudaÃ§Ã£o]
    T --> AB[Exibe erro]
    
    U --> AC[Limpa registradores]
    V --> AC
    W --> AC
    X --> AC
    Y --> AC
    Z --> C
    AA --> AC
    AB --> AC
    
    AC --> E
    
    subgraph "Sub-rotinas do LCD"
    LCD1[lcd_init - Inicializa LCD]
    LCD2[sendCharacter - Envia caractere]
    LCD3[posicionaCursor - Posiciona cursor]
    LCD4[clearDisplay - Limpa display]
    LCD5[escreveString - Escreve string]
    LCD6[delay - Atraso curto]
    LCD7[delay_longo - Atraso longo]
    end
    
    subgraph "Sub-rotinas de Utilidade"
    UTIL1[LIMPAR_MEMORIA - Limpa toda a memÃ³ria]
    UTIL2[LIMPAR_REGISTRADORES - Limpa registradores]
    UTIL3[COMPARAR_STRING - Compara strings]
    UTIL4[MEDIR_TEMP - LÃª sensor de temperatura]
    end
```
